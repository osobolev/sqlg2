apply plugin: 'java'

apply from: '../../../../db.gradle'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets.main.java.srcDirs = ['src']

compileJava.options.encoding = 'UTF-8'

configurations { sqlg }

dependencies {
    compile project(':mapper')
    compileOnly 'com.github.osobolev.sqlg2:sqlg2-preprocess:5.0'
    compile 'com.github.osobolev.sqlg2:sqlg2-local-server:5.0'
    runtime driverClasspath

    sqlg 'com.github.osobolev.sqlg2:sqlg2-preprocess:5.0'
    sqlg driverClasspath
    sqlg project(':mapper')
}

task preprocess {
    doLast {
        ant.taskdef(name: 'sqlg', classname: 'sqlg2.Preprocess', classpath: configurations.sqlg.asPath)
        ant.sqlg(classpath: configurations.sqlg.asPath,
                 driverclass: jdbcDriver, dbclass: dbClass,
                 mapperclass: 'runtime.mapper.CustomMapperImpl',
                 runtimemapperclass: 'runtime.mapper.CustomRuntimeMapperImpl',
                 url: jdbcUrl, user: username, password: password,
                 implpack: 'wrapper', wrappack: 'wrapper',
                 srcroot: 'src', encoding: compileJava.options.encoding) {
            fileset(dir: 'src') {
                include(name: 'runtime/db/ConfigDB.java')
            }
        }
    }
}
preprocess.onlyIf { doPreprocess }
compileJava.dependsOn(preprocess)

task run(type: JavaExec, dependsOn: classes) {
    main = 'runtime.Test'
    classpath = sourceSets.main.runtimeClasspath
    workingDir = '..'
    args([jdbcDriver, jdbcUrl, username, password, dbClass].collect {
        it == '' ? '""' : it
    })
}
