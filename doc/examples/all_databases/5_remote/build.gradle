description = 'SQLG client/server example'

apply plugin: 'java'

apply from: '../../db.gradle'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets.main.java.srcDirs = ['src']

compileJava.options.encoding = 'UTF-8'

configurations { sqlg }

dependencies {
    compileOnly 'com.github.osobolev.sqlg2:sqlg2-preprocess:5.0'
    compile 'com.github.osobolev.sqlg2:sqlg2-remote-server:5.0'
    compile 'com.github.osobolev.sqlg2:sqlg2-remote-client:5.0'
    compile 'org.eclipse.jetty:jetty-servlet:9.4.24.v20191120'
    runtime driverClasspath

    sqlg 'com.github.osobolev.sqlg2:sqlg2-preprocess:5.0'
    sqlg driverClasspath
}

task preprocess {
    doLast {
        ant.taskdef(name: 'sqlg', classname: 'sqlg2.Preprocess', classpath: configurations.sqlg.asPath)
        ant.sqlg(classpath: configurations.sqlg.asPath,
                 driverclass: jdbcDriver, dbclass: dbClass,
                 url: jdbcUrl, user: username, password: password,
                 implpack: 'wrapper', wrappack: 'wrapper',
                 srcroot: 'src', encoding: compileJava.options.encoding) {
            fileset(dir: 'src') {
                include(name: 'remote/dao/RemoteDAO.java')
            }
        }
    }
}
preprocess.onlyIf { doPreprocess }
compileJava.dependsOn(preprocess)

task runServer(type: JavaExec, dependsOn: classes) {
    main = 'remote.Server'
    classpath = sourceSets.main.runtimeClasspath
    args([jdbcDriver, jdbcUrl, dbClass].collect {
        it == '' ? '""' : it
    })
}

task runClient(type: JavaExec, dependsOn: classes) {
    main = 'remote.Client'
    classpath = sourceSets.main.runtimeClasspath
    args([username, password].collect {
        it == '' ? '""' : it
    })
}

task run(type: JavaExec, dependsOn: classes) {
    main = 'remote.Main'
    classpath = sourceSets.main.runtimeClasspath
    args([jdbcDriver, jdbcUrl, username, password, dbClass].collect {
        it == '' ? '""' : it
    })
}
