description = 'SQLG in J2EE example'

apply plugin: 'java'
apply plugin: 'war'

apply from: '../../../db.gradle'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets.main.java.srcDirs = ['src']

compileJava.options.encoding = 'UTF-8'

configurations { warDeps }
configurations { sqlg }

dependencies {
    compileOnly 'com.github.osobolev.sqlg2:sqlg2-preprocess:5.0'
    compile 'com.github.osobolev.sqlg2:sqlg2-runtime:5.0'
    runtime driverClasspath
    warDeps 'com.github.osobolev.sqlg2:sqlg2-runtime:5.0'
    warDeps sourceSets.main.output

    sqlg 'com.github.osobolev.sqlg2:sqlg2-preprocess:5.0'
    sqlg driverClasspath
}

task preprocess {
    doLast {
        ant.taskdef(name: 'sqlg', classname: 'sqlg2.Preprocess', classpath: configurations.sqlg.asPath)
        ant.sqlg(classpath: configurations.sqlg.asPath,
                 driverclass: jdbcDriver, dbclass: dbClass,
                 url: jdbcUrl, user: username, password: password,
                 implpack: 'wrapper', wrappack: 'wrapper',
                 srcroot: 'src', encoding: compileJava.options.encoding) {
            fileset(dir: 'src') {
                include(name: 'j2ee/dao/TestDAO.java')
            }
        }
    }
}
preprocess.onlyIf { doPreprocess }
compileJava.dependsOn(preprocess)

war {
    archiveName 'sqlg2.war'
    classpath = configurations.warDeps
    webAppDirName = 'jsp'
    webXml = file('web/web.xml')
}

task run {
}
